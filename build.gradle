description 'Spring框架系列'

// 构建Gradle脚本自身需要的资源，可以声明的资源包括依赖项、第三方插件、maven仓库地址等。
buildscript {
    ext {
        springBootVersion = '2.6.1'
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

// 全局配置，作用于所有子模块
allprojects {
    group project_group
    version project_version

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    repositories {
        maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'https://repo.spring.io/milestone' }
        mavenCentral()
    }
}

apply from: 'dependencies.gradle'
def env = System.getProperty("dev") ?: 'dev'

// 配置所有子项目
subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library' // api
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = project_jdk
    targetCompatibility = project_jdk

    // 公共依赖
    dependencies {
        implementation 'io.rest-assured:rest-assured'
        implementation 'org.slf4j:slf4j-api'
        implementation 'ch.qos.logback:logback-classic'
        implementation 'ch.qos.logback:logback-core'
        implementation 'org.slf4j:jcl-over-slf4j'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.junit.vintage:junit-vintage-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.assertj:assertj-core'
        testImplementation 'org.hamcrest:hamcrest'
        testImplementation 'org.hamcrest:hamcrest-all'
        implementation 'org.jetbrains:annotations'
        implementation 'org.projectlombok:lombok'
        testImplementation 'org.mockito:mockito-core'
        testImplementation 'org.apache.maven.surefire:surefire-logger-api'

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        testCompileOnly 'org.projectlombok:lombok:1.18.22'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    dependencyManagement {
        dependencies {
            for (depJar in rootProject.ext.dependencies) {
                dependency depJar.value
            }
        }
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    sourceSets {
        main {
            java { srcDirs = ['src/main/java'] }
            resources { srcDirs = ['src/main/resources', "src/main/profiles/$env"] }
        }
        test {
            java { srcDirs = ['src/test/java', 'src/test/groovy'] }
            resources { srcDirs = ['src/test/resources'] }
        }
    }

    test {
        useJUnitPlatform()
    }

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
}